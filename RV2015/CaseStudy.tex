\section{Examples}\label{sec:example}

In this section,  we will present several examples of
\texttt{copilot-kind} applied to verify properties on Copilot
monitors.


 First, let us reexamine the Copilot program from
 Section~\ref{sec:co-intro} that generates the  Fibonacci
 sequence. A fundamental property of this program is that it produces
 a stream of values that are always positive. We express this as follows:
\begin{lstlisting}[frame=single]
spec = prop "pos" (fib > 0)
  where
    fib :: Stream Word64
    fib = [1, 1] ++ (fib + drop 1 fib)
\end{lstlisting}
This  invariant property is clearly inductive and is easily
discharged. Note that, as mentioned in Section~\ref{sec:structure}, 
64-bits words are modelled by integers and eventual overflow problems are ignored here.
 
The next example uses \texttt{copilot-kind} to prove properties
relating two different specifications. Consider the following
specification:
\begin{lstlisting}[frame=single]
intCounter :: Stream Bool -> Stream Word64
intCounter reset = time
  where 
    time = if reset then 0
           else [0] ++ if time == 3 then 0 else time + 1
\end{lstlisting}
that acts as a counter performing modulo arithmetic, but is
reset when the \texttt{reset} stream value is true.  Now consider the
specification
\begin{lstlisting}[frame=single]
greyTick :: Stream Bool -> Stream Bool
greyTick reset = a && b
  where
    a = (not reset) && ([False] ++ not b)
    b = (not reset) && ([False] ++ a)
\end{lstlisting}
A quick back of the envelope calculation reveals that,
after a reset, \texttt{greyTick}'s output stream forms a cycle of
boolean values with the third item in the cycle having value
\texttt{true} and the rest being \texttt{false}.  Thus the two
specifications both have a cyclic structure and with a cycle that
begins when the \texttt{reset} stream is set to \texttt{true}.  

From the above observations we 
conjecture that given the same input stream, when \texttt{reset} is
true,  the \texttt{intCounter} is $0$ and \texttt{greyTick} is
\texttt{true} when \texttt{intCounter} is $2.$ We formalize these two
properties in our framework as follows:
\begin{lstlisting}[frame=single]
spec = do
  prop "iResetOk"   (r ==> (ic == 0))
  prop "eqCounters" (it == gt)
  where
    ic = intCounter r
    it = ic == 2
    gt = greyTick r
    r  = extern "reset" Nothing
\end{lstlisting}
These predicates are discharged using the proof scheme
\begin{lstlisting}[frame=single]
scheme :: ProofScheme
scheme = do
  check "iResetOk"
  check "eqCounters"
\end{lstlisting}  
\subsection{Boyer-Moore Majority Vote}~\label{sec:mvote}
Earlier research on Copilot  has investigated fault-tolerant
runtime verification~\cite{pike-isse-13}. In this work, we employed the Boyer-Moore
Majority Vote Algorithm~\cite{MooreBoyer82,Hesselink2005} to determine
if in a given list of votes there is a candidate that has more than
half of the votes, and if so, of finding this candidate. The algorithm
operates in two passes, first it chooses a candidate and the second
pass verifies that the candidate is indeed a majority.  The algorithm
is subtle and the desire to apply formal verification to our Copilot
implementation helped motivate the effort described here.   


%\begin{figure*}[ht]
%\begin{lstlisting}[frame=single]
%majority :: (Eq a, Typed a) => [Stream a] -> Stream a
%majority []     = error "majority: empty list!"
%majority (x:xs) = majority' xs x 1

%majority' []     can _   = can
%majority' (x:xs) can cnt =
% local
%    (if cnt == 0 then x else can) $
%      \ can' ->
%        local (if cnt == 0 || x == can then cnt+1 else cnt-1) $
%          \ cnt' ->
%           majority' xs can' cnt'
%\end{lstlisting}
%\caption{The first pass of the majority vote algorithm in Copilot.}
%\label{fig:majority}
%\end{figure*}

%\begin{figure*}[ht]
%\begin{lstlisting}[frame=single]
%aMajority :: (Eq a, Typed a) => [Stream a] -> Stream a -> Stream Bool
%aMajority xs can = aMajority' 0 xs can > (fromIntegral (length xs) `div` 2)
%
%aMajority' cnt []     _   = cnt
%aMajority' cnt (x:xs) can =
%  local
%    (if x == can then cnt+1 else cnt) $
%      \ cnt' ->
%        aMajority' cnt' xs can
%\end{lstlisting}
%\caption{The second pass of the majority vote algorithm in Copilot.}
%\label{fig:amajority}
%\end{figure*}

%The Copilot implementations of the first a
%pass is  given in Figure \ref{fig:majority} and Figure \ref{fig:amajority}
%respectively. 


Two versions of this algorithm were checked with
\texttt{copilot-kind}. The first algorithm was the one implemented as
part of the aforementioned research on fault tolerance and flew on a
small unmanned aircraft. This algorithm 
is a parallel implementation, where at each tick, the
algorithm takes $n$ inputs from $n$ distinct streams and is fully
executed. The second version of the algorithm  is a sequential version, where the inputs are
delivered one by one in time and where the result is updated at each
clock tick. Both can be checked with the basic k-induction algorithm,
but the proofs involved are of different natures.


\subsubsection{The parallel version.} The core of the algorithm is the
following:

\begin{lstlisting}[frame=single]
majorityVote :: (Typed a, Eq a) => [Stream a] -> Stream a
majorityVote [] = error "empty list"
majorityVote (x : xs) = aux x 1 xs
  where
  aux p _s [] = p
  aux p s (l : ls) =
    local (if s == 0 then l else p) $ \ p' ->
    local (if s == 0 || l == p then s + 1 else s - 1) $ \ s' ->
    aux p' s' ls
\end{lstlisting} 
Let us denote $A$ as the set of the elements that can be used as
inputs for the algorithm.  Assume $l$ is a list and $a \in A$, we
denote $|l|_a$ as the number of occurences of $a$ in $l$. The total
length of a list $l$ is simply written $|l|$. The
\texttt{majorityVote} functions takes a list of streams $l$ as its
input and returns an output $maj$ such that : \[ \forall a \in A, \;
\left( a \neq maj \right) \Longrightarrow \left(|l|_a \leq |l| /
  {2}\right) \] Given that quantifiers are handled poorly by SMT
solvers and their use is restricted in most model-checking tools,
including \texttt{copilot-kind}, we use a simple trick to write
and check this property.  If $P(n)$ is a predicate of an integer $n$,
we have $\forall n \, . \, P(n)$ if and only if $\neg P(n)$ is
satisfiable, where $n$ an unconstrained integer, which can be solved
by a SMT solver. The corresponding copilot specification can be
written as :
\begin{lstlisting}[frame=single]
okWith :: (Typed a, Eq a) => 
          Stream a -> [Stream a] -> Stream a -> Stream Bool
okWith a l maj = (a /= maj) ==> ((2 * count a l) <= length l)
  where
    count _e [] = 0
    count e (x : xs) = (if x == e then 1 else 0) + count e xs

spec = prop "OK" (okWith (arbitraryCst "n") ss maj)
  where
    ss = [ arbitrary ("s" ++ show i) | i <- [1..10]]
    maj = majorityVote
\end{lstlisting}
The function \texttt{arbitrary} is provided by the \texttt{copilot-kind} standard library and introduces an arbitrary stream. In the same way, \texttt{arbitraryCst} introduces a stream taking an unconstrained but constant value.

Note that we prove the algorithm for a fixed number of $N$ inputs
(here $N=10$). Therefore no induction is needed for the proof and the
invariant of the Boyer-Moore algorithm does not need to be made explicit. However, the size of the problem discharged to the SMT solver grows in proportion to $N$.


\subsubsection{The serial version.} Now, we discuss an implementation
of the algorithm where the inputs are read one by one in a single
stream and the result is updated at each clock tick. As the number of
inputs of the algorithm is not bounded anymore, a proof by induction
is necessary and the invariant of the Boyer-Moore algorithm, being
non-trivial, has to be stated explicitly. As stated in
Hesselink~\cite{Hesselink2005}, this invariant is :
\[ \begin{array}{c}
\forall m \in A, \;\;\; \left(m \neq p\right) \Longrightarrow \left( s + 2|l|_m \,\leq\, |l| \right) \;\; \wedge \;\; \left(m = p\right) \Longrightarrow \left( 2|l|_m \,\leq\, s + |l| \right)
\\

\end{array} \]
where $l$ is the list of processed inputs, $p$ is the intermediary result and $s$ is an internal state of the algorithm. The problem here is that the induction invariant needs universal quantification to be expressed. Unfortunately, this quantifier cannot be removed by a similar trick like the one seen previously. Indeed, when an invariant is of the form $\forall x. P(x, s)$, $s$ denoting the current state of the world, the induction formula we have to prove is :
\[ \forall x. P(x, s) \,\wedge\, T\left(s, s' \right) \,\models\, \forall x. P(x, s') \]
Sometimes, the stronger entailment 
\[ P(x, s) \,\wedge\, T\left(s, s' \right) \,\models\, P(x, s') \]
holds and the problem becomes tractable for the SMT solver, by replacing a universally quantified variable by an unconstrained one. In our current example, it is not the case. 


Our solution to the problem of dealing with quantifiers is restricted to the
case where $A$ is finite and replace each formula of the form $\forall x \in A
\; P(x)$ by $\bigwedge_{x \in A} P(x)$. This can be done with the help of the
\texttt{forAllCst} function provided by the copilot-kind standard library. It is
defined as :
\begin{lstlisting}[frame=single]
forAllCst ::(Typed a) => 
  [a] -> (Stream a -> Stream Bool) -> Stream Bool
forAllCst l f = conj $ map (f . constant) l
  where conj = foldl (&&) true
\end{lstlisting}
The code for the serial Boyer-Moore algorithm and its specification is then :

\begin{lstlisting}[frame=single]
allowed :: [Word8]
allowed = [1, 2]

majority :: Stream Word8 -> (Stream Word8, Stream Word8, Stream Bool)
majority l = (p, s, j)
  where
    p  = [0] ++ if s <= 0 then l else p
    s  = [0] ++ if p == l || s <= 0 then s + 1 else s - 1
    k  = [0] ++ (1 + k)
    
    count m = cnt
      where cnt = [0] ++ if l == m then cnt + 1 else cnt
    
    j = forAllCst allowed $ \ m ->
          local (count m) $ \ cnt ->
          let j0 = (m /= p) ==> ((s + 2 * cnt) <= k)
              j1 = (m == p) ==> ((2 * cnt) <= (s + k))
          in j0 && j1

spec = do
  prop "J" j
  prop "inRange" (existsCst allowed $ \ a -> input == a)
  where
    input = externW8 "in" Nothing
    (p, s, j) = majority input

scheme = assuming ["inRange"] $ check "J"
\end{lstlisting}

As the reader can see, we make the hypothesis that all the elements
manipulated by the algorithm are in the set \texttt{allowed}, which is
finite. As this set grows, the proofs discharged to the SMT solver
becomes larger, which is why this solution
is not scalable and has limited interest in practice.
